// SPDX-License-Identifier: MIT OR Apache-2.0
pragma solidity ^0.8.3;

import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract OPVFactory is
    ERC721,
    AccessControlEnumerable,
    ERC721Enumerable,
    Ownable
{
    using Counters for Counters.Counter;
    Counters.Counter private _itemIds;

    mapping(address => bool) public approvalWhitelists;
    mapping(uint256 => bool) public lockedTokens;
    mapping(uint256 => address) public creators;

    string private _baseTokenURI;
    string private _marketplaceAddress;

    uint256 public _feeMint;

    address public _feeAddress;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");

    IERC20 public OPV;

    event logMint(address ownerAddress, uint256 tokenId, uint256 uniqueId);

    constructor(
        string memory defaultUri,
        address marketplaceAddress,
        address opvAddress
    ) ERC721("OpenLive NFT Factory", "OPVF") {
        OPV = IERC20(opvAddress);
        _baseTokenURI = defaultUri;
        addApprovalWhitelist(marketplaceAddress);
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    function creatorOf(uint256 tokenId) external view returns (address) {
        return creators[tokenId];
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     */
    function mint(address to, uint256 uniqueId) public virtual {
        _itemIds.increment();
        uint256 tokenId = _itemIds.current();

        if (hasRole(MINTER_ROLE, _msgSender())) {
            require(!_exists(tokenId), "Must have unique tokenId");
        } else {
            require(
                OPV.balanceOf(_msgSender()) >= _feeMint,
                "Mint NFT : Unavailable by balanceOf"
            );
            require(!_exists(tokenId), "Must have unique tokenId");
            OPV.transferFrom(_msgSender(), _feeAddress, _feeMint);
        }
        creators[tokenId] = _msgSender();
        _mint(to, tokenId);
        emit logMint(to, tokenId, uniqueId);
    }

    function setFeeAddress(address feeAddress) public onlyOwner {
        _feeAddress = feeAddress;
    }

    function setFee(uint256 amount) public onlyOwner {
        _feeMint = amount;
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator)
        public
        view
        override
        returns (bool)
    {
        if (approvalWhitelists[operator] == true) {
            return true;
        }

        return super.isApprovedForAll(owner, operator);
    }

    /**
     * @dev Allow operation to reduce gas fee.
     */
    function addApprovalWhitelist(address proxy) public onlyOwner {
        require(approvalWhitelists[proxy] == false, "Invalid proxy address");

        approvalWhitelists[proxy] = true;
    }

    /**
     * @dev Remove operation from approval list.
     */
    function removeApprovalWhitelist(address proxy) public onlyOwner {
        approvalWhitelists[proxy] = false;
    }

    /**
     * @dev Lock token to use in game or for rental
     */
    function lock(uint256 tokenId) public {
        require(
            approvalWhitelists[_msgSender()],
            "Must be valid approval whitelist"
        );
        require(_exists(tokenId), "Must be valid tokenId");
        require(!lockedTokens[tokenId], "Token has already locked");
        lockedTokens[tokenId] = true;
    }

    /**
     * @dev Unlock token to use blockchain or sale on marketplace
     */
    function unlock(uint256 tokenId) public {
        require(
            approvalWhitelists[_msgSender()],
            "Must be valid approval whitelist"
        );
        require(_exists(tokenId), "Must be valid tokenId");
        require(lockedTokens[tokenId], "Token has already unlocked");
        lockedTokens[tokenId] = false;
    }

    /**
     * @dev Get lock status
     */
    function isLocked(uint256 tokenId) public view returns (bool) {
        return lockedTokens[tokenId];
    }

    /**
     * @dev Set token URI
     */
    function updateBaseURI(string calldata baseTokenURI) public onlyOwner {
        _baseTokenURI = baseTokenURI;
    }

    /**
     * @dev See {IERC165-_beforeTokenTransfer}.
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721, ERC721Enumerable) {
        require(!lockedTokens[tokenId], "Can not transfer locked token");
        super._beforeTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override(AccessControlEnumerable, ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
}